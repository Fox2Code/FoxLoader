// Final build.gradle
group 'com.fox2code'
version project['foxloader.version']

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    compileOnly 'com.google.code.gson:gson:2.10.1'
    include(project(":common")) {
        transitive = false
    }
    include(project(":client")) {
        transitive = false
    }
    include(project(":server")) {
        transitive = false
    }
    include(project(":fallbacks")) {
        transitive = false
    }
}

jar.dependsOn(":betacraft:jar")
jar.dependsOn(":client:jar")
jar.dependsOn(":common:jar")
jar.dependsOn(":server:jar")
jar.dependsOn(":fallbacks:jar")

jar {
    from "LICENSE"

    manifest {
        attributes 'Main-Class': 'com.fox2code.foxloader.installer.Main'
        attributes 'FoxLoader-Transformer-Version':
                project['foxloader.lastReIndevTransformerChanges']
        attributes 'FoxLoader-Version': project['foxloader.version']
        attributes 'ReIndev-Version': project['reindev.version']
        // Only supported on Java9+ but only needed on Java9+! :3
        attributes 'Launcher-Agent-Class': 'com.fox2code.foxloader.launcher.DependencyHelper$Agent'
        attributes 'Premain-Class': 'com.fox2code.foxloader.launcher.DependencyHelper$Agent'
    }
}

afterEvaluate {
    jar {
        from(configurations.include.collect { it.isDirectory() ? it : zipTree(it) }) {
            exclude("module-info.class")
        }
        from(project(":betacraft").buildDir.getAbsolutePath() + File.separator + "libs") {
            include("betacraft-" + project['foxloader.version'] + ".jar")
        }
    }
}